=== scene_3 ===
// Вступительное событие
Команда "Раптора" быстро посовещалась, меняясь на "мостике" и оставляя друг другу сообщения в логах системы. Что же они решили?
*Будем бороться до конца! -> victory
*Наверное, {pirate} правы. Нет причин цепляться за бессмысленную миссию...-> melancholic

= victory
Конечно, на наш "Раптор" попали отборные смельчаки, они решили бороться до последнего миллиампер-часа!
-> call
=melancholic
Как вы могли такое подумать? Команда нашего "Раптора" совсем не такая, они решили бороться до последнего миллиампер-часа!
-> call
=call
#location: pirate_planet
Коммуникатор корабля вдруг опять заработал на приём сигнала, причём не дожидаясь вашего подтверждения приёма. В терминале появилась запись: 
СИСТЕМА: Входящее сообщение. Протокол связи: Аварийный.
Сначала вы испугались, что сейчас ваши системы хакнут, но, похоже, эти данные просто логи? Через несколько секунд изучения данных всё стало ясно. Это активировался аварийный маячок с обломков "Раптора", который {pirate} разобрал на запчасти. Энергоблок его корабля дошёл до критических параметров и начал пытаться спасти информацию, передавая её в ближайший приёмник с заводскими настройками.
Похоже, что после подключения к новому кораблю он начал бэкапить все их логи, включая переговоры между собой, так что тут можно найти массу всего интересного. Алгоритм сразу выявил, что пираты несколько раз друг для друга цитировали один и тот же текст, явно не их авторства.
"Раптор"-1: «Всем рапторам! Объект b системы Росс 128 — пригоден для размещение земной колонии. Состав атмосферы близок к земному, большое количество жидкой воды подтверждено. Передача информации непосредственно на Землю невозможна из-за слабости антенны наших кораблей. Каждый корабль класса "Раптор" обязан передать данные следующему. Цепь должна быть непрерывна, пока сигнал не достигнет Земли. Сохраняйте курс. Работайте спокойно. 
"Раптор"-1: На случай нехватки ресурсов для продолжения миссии передаем параметры орбиты объекта в системе звезды Вольф 359 оптимального для ремонтов раптора: атфмосфера непригодна для людей, но защитит раптор от радиации и даст зарядить батареи, а на поверхности легко найти нужные нам элементы »

Что же, похоже, непригодность антенн для межзвёздных сигналов — это правда, хотя догадался до этого не сам {pirate}. Они "проснулись" из-за полученного сигнала, но решили, что Земля не ответит. Что передавать — бессмысленно. Им важнее было остаться "живыми". Дальше там куча переговоров, невозможно ничего понять без сноровки. Выберем, кто будет анализировать остальные данные.
// Выбор активного персонажа
-> select
= select
//Выбор активного персонажа
->select_current_char(->selected_current)

= selected_current
Выбран персонаж: {char_name(current_char)}. Число = {char_laser(current_char)}. Предыстория: {char_desc(current_char)}.
+ Продолжить -> continue
+ Выбрать другого -> select
= continue
Можно всё просмотреть быстро, пока {pirate} не слишком сократил дистанцию. Или дать себе время разобраться во всех переговорах тщательно.
//Тут происходит выбор скорости
*Быстро просмотреть логи маячка.
    ~ current_speed = 1
*Погрузиться в расшифровку, даже если придётся рискнуть временем.
    ~ current_speed = 0
- -> check_base

= check_base
{char_name(current_char)} подключается к передаче. Тут правда куча переговоров и технических логов, всё вперемешку. Непохоже, что разработчик планировал сам в чём-то разобраться, гадая по этой каше. { pressure == -1: От анализа данных отвлекают мысли о уязвимости Раптора перед пиратами, и хочется зацепится за какую-то соловинку в этих данных. | Узнаем, удалось ли узнать что-то полезное?}
// Проверка на FEELINGS, специализация: Анализ неструктурированной информации, скорость: быстро
{ccplayer_roll(1, unstruct,pressure,false):
    -> success_base
- else:
  -> fail_base 
}
= success_base
Среди кучи телеметрии с разных подсистем {char_name(current_char)} видит главное: расчёт момента, когда {pirate} собираются сбросить аккумулятор и совершить рывок. И что не менее важно, в данных нашлись команды для самого маячка — можно включить световые элементы и сбить показания звёздного датчика. Без него не получится совершить идеальный маневр. И как раз вовремя! Судя по данным, {pirate} должен начать маневр через несколько мгновений!
*Активировать сигналы маячка!
    Сработало! Яркая вспышка зажглась всего на пару секунд, но этого хватило, чтобы {pirate} дезориентировался в пространстве. Он даёт импульс и сбрасывает ненужный теперь аккумулятор, но ему придётся ещё много маневрировать, чтобы ваши траектории могли пересечься.
-
Теперь можно попробовать ещё немного почитать полученные логи, вдруг там найдётся ещё что-то полезное?
//Тут происходит выбор скорости
*Бросить ещё один быстрый взгляд.
    ~ current_speed = 1
*Изучить остаток логов обстоятельно.
    ~ current_speed = 0
- -> bonus_check

= fail_base
К сожалению, в логах не нашлось ничего особо полезного. Там были грустные истории, как {pirate} разобрали уже 4 корабля, которые пролетали отсюда гораздо дальше нашего "Раптора", но меняли траекторию и попадали в ловушку. Но ничего, что помогло бы противостоять им сейчас.
От чтения логов пришлось отвлечься: {pirate} начал какой-то маневр и явно готовится отбросить один из аккумуляторов. Нужно срочно что-то сделать.
Вдруг {char_name(current_char)} видит в начале пакета данных сообщение со списком команд, которые можно отправить на аварийный маяк. { config ==1: Используем их же конфигурацию передатчка для отправки этой команды.| Может, какая-то из них поможет?}
//Тут происходит выбор скорости
*Попробовать быстро отправить все команды подряд.
    ~ current_speed = 1
*Почитать список и выбрать подходящую команду.
    ~ current_speed = 0
- -> save_check

= bonus_check
// Проверка на FEELINGS, специализация: Переговоры, скорость: медленно
{ccplayer_roll(0, negotiation,0,false):
    -> success_bonus
- else:
  -> fail_bonus  
}

= success_bonus
Вы наткнулись на длинную цепочку переговоров между членами экипажа после того, как {pirate} получили сообщение от "Раптора"-1. Не так-то просто разобраться в их сути, но всё же {char_name(current_char)} с ней справляется. Оказывается, не все ваши противники сразу приняли решение заняться пиратством: они очень долго сомневались и обсуждали, как же они должны поступить, перепроверяя раз за разом свои рассуждения.
Это сильно просадило батарею корабля, поэтому они не смогли подлететь поближе к звезде и зарядиться. Это их очень испугало, и они приняли решение отключить возможность переобдумывать свои поступки: теперь они всегда действуют быстро и ни в чём не сомневаются.
Остатки записей маячка вы оставили анализироваться в фоновом режиме: результат не будет быстрым, но точно ничего не будет пропущено.
// Бонус для сцены 5
~ reflection = 1
-> scene_4

= fail_bonus
Вы пытались найти ещё хоть что-то об их планах, но похоже большая часть записей — это просто куча бесполезной телеметрии разных подсистем, которые сохраняли и перезаписывали отважный маячок в стане врага. В итоге вы решили остававить эти записи анализироваться в фоновом режиме: результат не будет быстрым, но точно ничего не будет пропущено.
-> scene_4

= save_check
// Проверка на LASERS, специализация: Ремонт / техника, скорость: быстро
{ccplayer_roll(1, repair,config,true):
    -> success_save
- else:
  -> fail_save  
}
= success_save
Среди команд нашлась одна, которая на несколько секунд зажгла свет аварийного маячка, и как раз вовремя: {pirate} дезориентировался и закрутился на месте. Импульс и отброшенный аккумулятор помогли им набрать скорость, но полетели они не в вашу сторону.
Остатки записей маячка вы оставили анализироваться в фоновом режиме: результат не будет быстрым, но точно ничего не будет пропущено.
-> scene_4

= fail_save
Маячок в ответ на ваши вызовы даёт свою последнюю короткую вспышку и гаснет навсегда. Увы, это никак не помогло. Батарея уже была отброшена, резкий импульс — и пиратский корабль ускоряется, переходя на траекторию перехвата.
Остатки записей маячка вы оставили анализироваться в фоновом режиме: результат не будет быстрым, но точно ничего не будет пропущено. Пока что вам не до анализа.
// штраф к сцене 4
~ snatch = -1
-> scene_4
