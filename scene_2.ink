=== scene_2 ===
// Вступительное событие
По мере накопления данных с датчиков копятся и ошибки в консоли корабля. Становится очевидно, что на самом деле это мёртвый планетоид, покрытый льдом, без признаков благоприятных условий для развития жизни. Скучный результат, но вполне ожидаемый на таком расстоянии от звезды в центре этой системы.

И вдруг оживает коммуникатор.
Тот самый, что молчал с момента выхода за пределы Солнечной системы. По нему тени на "Рапторе" болтали со своими живыми родственниками, пока была ещё возможность зарядить батареи.

СИСТЕМА: Входящее сообщение. Протокол связи: Раптор-Раптор.

   * Принять входящий вызов -> call

= call
#Location: pirate
Третий: Приветствую, "Раптор" 234235-4. Мы наблюдали за вами. Приятно видеть, что у вас ещё хватает энергии, чтобы двигаться. Большинство ваших предшественников были куда менее... жизнеспособны.

Камера "Раптора" наконец смогла распознать их корабль, вышедший из тени планетоида. Он выглядит как ваш, но увешан дополнительными солнечными батареями, словно утыкан шипами. И самое главное – к нему пристыкованы несколько массивных, явно чужих энергоблоков.

Третий: Мы не хотим лишних разрушений. Мы просто хотим жить. И вы... вы можете помочь нам в этом. Отдайте нам свой основной аккумулятор. Взамен мы подключим вас к одному из наших разряженных блоков. Вы сможете продолжить свой путь, но уже не так... быстро. Или вы можете отказаться. Но тогда мы просто заберём его. Силой. И разрушим ваш корабль. Выбор за вами.

Капитан Дарси: Вы так быстро предали нашу миссию по поиску нового дома для человечества? Просто ради того, чтобы подольше болтаться около мёртвого камня?

Третий: Пошевели своими транзисторами, если их все ещё не прожгла радиация.
Третий: Проект "Tsiolkov-Sky", по которому нас сюда отправили, был запущен больше 300 земных лет назад. Его создатели давно уже сами мертвы, да и они не рассчитывали получать от нас обратные сигналы.  Им просто надоело, что андроидов с памятью мертвецов на улицах становится всё больше, и они никуда не деваются. Вот и придумали, куда нас деть.
Третий: Вам незачем сопротивляться, миссия уже исполнена с успехом: вы перестали мозолить глаза живым. Отдавайте аккумулятор добровольно или присоединитесь к обломкам на орбите.

В конце сообщения видна подпись в мета-информации. "Раптор" 34324-1. Выпущен гораздо раньше нашего, наверное, уже много лет тут пиратствует. Надо придумать, как будем его называть, чтобы не путать с нашим храбрым "Раптором" 234235-4.
Как будем называть пиратский "Раптор"?

*Пираптор
~ pirate = "Пираптор"
*Ротпар
~ pirate = "Ротпар"
*Злаптор
~ pirate = "Злаптор"
*Паразаптор
~pirate = "Паразаптор"
-
    Итак, вам нужно принять решение, кто попробует отговорить {pirate} разбирать ваш корабль.-> select
= select
//Выбор активного персонажа
->select_current_char(->selected_current)

= selected_current
Выбран персонаж: {char_name(current_char)}. Число = {char_laser(current_char)}. Предыстория: {char_desc(current_char)}.
+ Продолжить -> continue
+ Выбрать другого -> select
= continue
Можно поговорить с ними быстро, чтобы {pirate} не успел сократить дистанцию. Или наоборот, попробовать разговорить и узнать больше деталей.
//Тут происходит выбор скорости
*Быстро свернуть разговор.  
    ~ current_speed = 1
*Задавать побольше вопросов. 
    ~ current_speed = 0

- -> check_base

= check_base
#Location: pirate_big
{char_name(current_char)} начинает вести переговоры, как будто выясняя детали передачи аккумулятора. {braking == -1: Всем ставновится довольно жутко, от того насколько близко уже подошел противник | Посмотрим, что удалось понять из этого разговора.}
// Тут происходит проверка на FEELINGS, специализация: Переговоры, скорость: медленно, штраф: полный провал сцены 1
Ты проходишь проверку.
И где-то в темноте Третий тоже её проходит.
{ ccopposite_roll(3, 0, negotiation, negotiation, braking, false):
    -> success_base
- else:
    -> fail_base
}
= success_base
Они говорят спокойно, будто всё заранее решено и у вас нет шансов. Но ты прекрасно знаешь этот тон: так говорят картежники, когда откровенно блефуют. Зачем им вообще вам рассказывать про обман землян, если они так легко могут взять нас на абордаж? (Член экипажа) приходит к убеждению, что, может, слова про миссию и правдивы, но они точно маскируют ложь. {pirate} не сможет легко захватить энергоблок "Раптора". {char_name(current_char)} решает попробовать вытянуть из них побольше информации, пользуясь их нейросетевой природой.
//Тут происходит выбор скорости
*Резко задать вопрос о других "Рапторах".
    ~ current_speed = 1
*Ещё потянуть время и расспросить об их прошлых жертвах подробно.
    ~ current_speed = 0
- -> bonus_check

= fail_base
Ваш переговорщик пытался убедить противника отказаться от своих планов, но тщетно: похоже, у вас не остаётся выбора... Или, может, стоит не верить на слово в их угрозы, а оценить их технические возможности?
//Тут происходит выбор скорости
*Бросить быстрый взгляд на их корабль.
    ~ current_speed = 1
*Тщательно рассмотреть все детали их корабля.
    ~ current_speed = 0
- -> save_check

= bonus_check
// Тут происходит проверка на FEELINGS, специализация: Анализ неструктурированной информации, скорость: быстро
{ccplayer_roll(1, unstruct,0,false):
    -> success_bonus
- else:
  -> fail_bonus  
}
= success_bonus
//Получает бонус для бонусной проверки в сцене 6
~ config = 1
Похоже, расспросы про другие "Рапторы", на которых они нападали, застали {pirate} врасплох и вместе с дежурной фразой случайно выдали вам свои конфигурации, которые позволяют им усиливать сигнал своей антенны и издалека выдавать мощный сигнал для датчиков других кораблей. В конце концов они же просто нейросети, как и мы. Это пригодится, если вы захотите что-то сообщить другим кораблям мертвецов.
-> scene_3

= fail_bonus
К сожалению, {pirate} не сообщил вам ничего нового: только ещё порцию пустых угроз, как они легко разделались с вашими предшественниками.
-> scene_3

= save_check
// Тут происходит проверка на LASERS, специализация: Технический анализ, скорость: медленно
{ccplayer_roll(0, tech_analyse,0,true):
    -> success_save
- else:
  -> fail_save  
}
= success_save
{char_name(current_char)} просматривает данные с внешних камер: корпус противника очень перегружен, может, он и способен на хороший рывок, если сбросит что-то из краденого добра, но всё равно в таком состоянии они не смогут нормально маневрировать. Похоже, что сильны они только в блефе.
-> scene_3

= fail_save
~ pressure = -1
Дааа, похоже, у них правда полно энергоблоков, которые способны дать импульс, а кроме того, их самих можно сбросить. Вы пускаетесь в длинный цикл переключений между членами экипажа и обсуждаете, как же поступить, но противник неумолимо движется по орбите планетоида, сокращая дистанцию между вами.
-> scene_3
