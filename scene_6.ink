=== scene_6 ===
// Вступительное событие
 #Location: accum
Теперь аккумулятор, полный энергии, болтается где-то в космосе. Слишком далеко, чтобы наши датчики могли отличить его от металлических астероидов. С другой стороны мы сильно потратились на то, чтобы гонять по космосу от пиратов. Без запасного аккумулятора будет туго. Придётся положиться на интуицию при выборе траектории. Кому доверим  проверку судьбы?
// Выбор активного персонажа
-> select
= select
//Выбор активного персонажа
->select_current_char(->selected_current)

= selected_current
Выбран персонаж: {char_name(current_char)}. Число = {char_laser(current_char)}. Предыстория: {char_desc(current_char)}.
+ Продолжить -> continue
+ Выбрать другого -> select
= continue
Отлично, теперь у {char_name(current_char)}  есть возможность вглядеться в звезды и принять решение какой из далеких обломоков — тот, что нам нужен.
// Выбор скорости 
*[Пробуем поддаться первом порыву.] 
    ~ current_speed = 1
*[Внимательно прислушаться к своей интуиции, не торопиться.]
    ~ current_speed = 0
-    -> check_base

= check_base
{char_name(current_char)} внимательно рассматривает показания звездного датичка. Если другие датчики не могут нам помочь, то придется полагаться на звезды.
Мы идём вслепую, но не впервые. {battery == 1: К счастью мы знаем, что аккмулятора уже летит в нашу сторону.|}

// Проверка на FEELINGS, специализация: интуиция, скорость: медленно, бонус — если был успех в сцене 5.
{ccplayer_roll(1, intuition,battery,false):
    -> success_base
- else:
  -> fail_base 
}
= success_base
#Location: accum_win
{char_name(current_char)} делает выбор и подает легкий импульс. Раптор меняет траекторию и летит. На мгновение кажется, что всё вокруг замерло. И вдруг — еле заметное свечение на периферии датчиков.   Она плывёт сквозь тьму, медленно вращаясь, — наша батарея.  Мы перехватываем еще без особых потерь.

Теперь мы можем и передать сообщение другим рапторам и сами отправится на одну из планет.{config == 1: Используем конфигурацию передатчиков от пиратов, чтобы передать сообщение как можно дальше.| }
//Тут происходит выбор скорости
*[Составить сообщение быстро, пока вы на эмоциональнмо подъеме.]
    ~ current_speed = 1
*[Вдумчиво поработать над вашим сообщением.]
    ~ current_speed = 0
- -> bonus_check
= fail_base
Нет, ничего. Только шум фона, и холодный блеск частиц пыли. Может мы пролетели мимо?
Надо  еще раз попробовать просканировать всеми датчиками.
//Тут происходит выбор скорости
*Провести сканирование как можно быстрее, пока не пролетели мимо. 
    ~ current_speed = 1
*Тщательно откалибровать датчики.
    ~ current_speed = 0
- -> save_check
= bonus_check
//Проверка на FEELINGS, специализация: переговоры, скорость: медленно, бонус — если был успех в сцене 2.
{ccplayer_roll(1, negotiation,config,false):
    ~ transmit = true
    -> choice
- else:
  -> choice
}
= save_check
{char_name(current_char)} запускает сканирование. Если мы недалеко от аккумулятора, то он должен найтись. {speedy == 1: Хорошо, что у нас есть запас скорости.| }
// Проверка на LASERS, специализация: Ремонт / техника, скорость: быстро
{ccplayer_roll(1, tech_analyse,speedy,true):
    -> success_save
- else:
  -> fail_save  
}
= success_save
#Location: accum_win
Есть! Мы немного с ним разменулись, но все же смогули совершить маневр и поймать его манипулятором. Мы тратим почти всю энергию на манёвр, но аккумулятор у нас. Теперь, конечно не выйдет передать что-то особо вдохновляющее, но не не менее.
Что будем делать? -> choice

= choice
*[Отправить сообщение о планете у Росс 128 — пригодной для людей.] -> ending_transmit_signal_earth
*[Отправить координаты орбиты у Вольфа 359 — места, где роботы смогут построить новый дом.] -> ending_transmit_signal_robots
*[Или может останемся здесь и займем вакантное место?] -> ending_become_pirates

= fail_save 
Сканирование ничего не дало. Последний импульс отражается от крошечных камней.  
Похоже, это всё. Придётся использовать то, что осталось. Что будем делать?
*[Вложим остатки энергии у нас в аккумуляторе, чтобы сообщение Раптора -1 продолжило свое движение к Земле.] -> ending_broadcast
*[Отправимся строить колонию, где могут найти свой дом другие рапторы и тени в них.] -> ending_robots
*[Или может останемся здесь и займем вакантное место?] -> ending_become_pirates




