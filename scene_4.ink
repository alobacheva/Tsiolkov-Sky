=== scene_4 ===
// Вступительное событие
#location: pogonya
После короткой, сбившейся паузы {pirate} сманеврировал и включил форсаж. Идут на перехват. Вообще-то «рапторы» не предназначены для космических сражений: они созданы для исследования планет, могут совершить посадку и даже попытаться начать обустройство колонии. Но не для того, чтобы атаковать друг друга, сходясь в невесомости, как рыцари. Тем не менее, это происходит. Ну что, капитан, кому доверим самый важный манёвр? 
// Выбор активного персонажа
-> select
= select
//Выбор активного персонажа
->select_current_char(->selected_current)

= selected_current
Выбран персонаж: {char_name(current_char)}. Число = {char_laser(current_char)}. Предыстория: {char_desc(current_char)}.
+ Продолжить -> continue
+ Выбрать другого -> select
= continue
{char_name(current_char)} может заранее начать изменять положение нашего «раптора», чтобы уйти от атаки, но {pirate} тоже может это заметить. Либо можно сделать рывок в сторону в последний момент — и, если повезёт, они не успеют повторить наш манёвр.
//Тут происходит выбор скорости
*Резко — ставим всё на скорость реакции. Уходим в бок в последний момент.
    ~ current_speed = 1
*Начинаем манёвр заранее — даже если они заметят, что мы двигаемся, им будет сложно постоянно корректировать курс.
    ~ current_speed = 0
- -> check_base

= check_base
{char_name(current_char)} перехватывает управление. На карту поставлено всё.
Где-то в темноте Второй {snatch == -1:уверенно идет на перехват.| тоже пытается совершить маневр.} {orbit == 1: К счастью мы уже неплохо ориентируемся около это глыбы льда.| к сожалению, приходится рассчитывать свое движение на незнакомой орбите.}

// Проверка на LASERS, специализация: навигация и пилотирование, скорость: быстро, бонус: полный успех в сцене 1, штраф: полный провал в сцене 3. (Оппозитно — Второй)
{ ccopposite_roll(4, 0, repair, navigation, snatch + orbit, true):
    -> success_base
- else:
    -> fail_base
}
= success_base
Маневровые движки вспыхнули коротко, почти бесшумно. «Раптор» скользнул вбок как раз вовремя — и {pirate} пронёсся мимо, едва не поцарапав обшивку своим абордажным крюком, собранным из манипулятора для сбора самородков на поверхности планет. Чтобы догнать нас, они сильно разогнались, и теперь на разворот им понадобится куча энергии.
А мы остались целы. И, кажется, даже можем попробовать сделать гравитационный манёвр около этого планетоида с разгоном.
#location: mimo
//Тут происходит выбор скорости
*Попробовать быстро скорректировать курс?
    ~ current_speed = 1
*Проанализировать параметры планеты и потратить время на расчёт баллистики.
    ~ current_speed = 0
- -> bonus_check
= fail_base
Импульс пришёл чуть позже, чем нужно. «Раптор» дёрнулся, потерял часть скорости — и противник моментально подстроился. Расстояние сокращается. Система тревоги пульсирует в такт тикам. Последний шанс спастись — вызвать их по связи и попробовать договориться.
//Тут происходит выбор скорости
*Сказать первое, что сгенерируется на процессоре
    ~ current_speed = 1
*Подготовить переговорную стратегию
    ~ current_speed = 0
   - -> save_check

= bonus_check
{char_name(current_char)} снова у меня на навигации. Эта карликовая планета впереди — старая ледяная сфера, тянет слабо, но движется быстро. Если рассчитать угол подхода, можно использовать её орбитальную скорость как рычаг — классический гравитационный манёвр, «разгон об орбиту».

Всё зависит от точности. Ошибка — и нас просто выкинет в тень звезды. Или в объятия {pirate}, что ещё хуже. {orbit == 1: Хорошо, что мы смогли предварительно проанализировать все объекты тут. | Двигаться приходится практические наощупь.}

// Проверка на LASERS, специализация: технический анализ, скорость: медленно, бонус — полный успех в сцене 1
{ccplayer_roll(0, tech_analyse,orbit,true):
    -> success_bonus
- else:
  -> fail_bonus 
}
= success_bonus
// Дает бонус в 6 сцене
~ speedy = 1
Корабль входит в зону притяжения под идеальным углом. Гравитация ледяной планеты цепляет нас на секунду, и весь корпус ощущает едва заметное сжатие — как будто пространство само толкает «в спину». Траектория сгибается, вектор скорости меняется. Двигатели молчат, а мы уже летим быстрее.
Теперь мы движемся быстрее, чем {pirate}. Можем ещё больше увеличить дистанцию между нами или даже догнать их, если захотим. У нас, в конце концов, тоже есть такой же манипулятор...
-> scene_5

= fail_bonus
Расчёт смещён всего на долю градуса. Корабль дрожит, автомат стабилизирует вращение, но импульс получился не в ту сторону, куда мы планировали. Что ж, по крайней мере теперь {pirate} точно не сможет нас догнать, как и мы их.
-> scene_5

= save_check
// Альтернативная проверка (FEELINGS), специализация: связь / переговоры, скорость: быстро (Оппозитно — Второй), бонус -полный успех в сцене 3
{char_name(current_char)} понимает, что единственное, на что может среагировать экипаж противников, — это угроза их «жизни». В «рапторах» есть команда самодетонации на крайний случай, и нужно показать, что этот случай уже наступил.{reflection == 1:Хорошо, что мы знаем их гораздо лучше, чем они нас.}
{ ccopposite_roll(4, 1, repair, negotiation, reflection, false):
    -> success_save
- else:
    -> fail_save
}

= success_save
Передача идёт короткой строкой без лишних слов. Только сухие данные: радиус взрыва, мощность наших батарей, расчёт вероятности поражения их корабля.
#location: mimo
На том конце — гробовое молчание. Потом происходит короткий импульс маневрового двигателя, и они летят мимо. Может, потом пожалеют, но вряд ли решатся тратить энергию на разворот. 
-> scene_5

= fail_save
#location: attac
Ответа нет. И они рядом — видимо, не верят, что кто-то способен отказаться даже от мгновений своего бессмертия в космосе. Можно не проводить дополнительный анализ: каждой из «теней» на корабле ясно — из этой точки не выбраться. Окончательное решение за капитаном.

Что будем делать?
*Активировать протокол самоуничтожения. «Раптор» закончит свой путь, но откроет дорогу для следующих кораблей за нами. -> ending_self_destruct
*Сдаться. Может, нас не отключат сразу. -> ending_surrender
*Вложить весь остаток энергии в передачу сообщений от «Раптора-1». Пусть мы отключимся, но, может, сигнал всё-таки дойдёт до Земли. -> ending_broadcast_pirat




