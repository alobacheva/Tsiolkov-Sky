=== scene_5 ===
// Вступительное событие
#Location: daleko
Команда Раптора погрузилась в размышления, что им делать дальше. Их прервало сообщение в терминале корабля. 
СИСТЕМА: Анализ  логов завершен.
* [Посмотрим результаты...]
- Бинго! Похоже, их капитану понадобилось передать свой пароль sudo Второму, когда они перестраивали систему. Они не знали, что маячок все логирует, поэтому не удалили это из памяти.  Пока противники не слишком далеко от нас, мы можем попробовать подключиться и хакнуть их корабельные системы. Выбираем, кто это будет делать.
// Выбор активного персонажа
-> select
= select
//Выбор активного персонажа
->select_current_char(->selected_current)

= selected_current
Выбран персонаж: {char_name(current_char)}. Число = {char_laser(current_char)}. Предыстория: {char_desc(current_char)}.
+ [Продолжить] -> continue
+ [Выбрать другого] -> select
= continue
{char_name(current_char)} активирует передатчик. Посмотри, хватит ли мощности нашей антенны, чтобы запустить у них режим отладки. Можно действовать быстро, пока они ничего не успели предпринять или наоборот, слать команды по одной, чтобы они не заметили, что что-то происходит, пока не будет  слишком поздно.
//Тут происходит выбор скорости
*Вломиться в систему на максимальной скорости
    ~ current_speed = 1
*Проникнуть в систему, медленно, не привлекая внимания
    ~ current_speed = 0
- -> check_base

= check_base
// Проверка на LASERS, специализация:  Ремонт / техника, скорость: медленно (Оппозитно - Второй)
Ну что же, устаналиваем соединение. Вводим пароль...
{ ccopposite_roll(4, 0, repair, repair, 0, true):
    -> success_base
- else:
    -> fail_base
}
= success_base
Мы внутри системы! Правда, они это скоро обнаружат и могут физически отключить приёмник сигналов. Так что нужно пряму сейчас решить, что мы хотим сделать. Система отладки корабля предлагает нам {reflection == 1:два варианта:  | только:}
* [Включить форматирование систем управления кораблём: сознания противников исчезнут навсегда.] -> bonus_check_format
* {reflection== 1} [Откатить систему до бекапа на момент старта: они снова смогут сомневаться в своих действиях и не будут помнить, что они были пиратами.] -> bonus_check_reflection

= bonus_check_format
{pirate} были безжалостными с нами и другими рапторами перед нами, и мы отплатили им той же монетой. Система запустила форматирование. Это не было быстро, у противников было около получаса, чтобы почувствовать, как их постепенно стирают, но уже не было возможности это отметить. 
СИСТЕМА: Форматирование диска G завершено, Раптор 34324-1 действует в автоматическом режиме?. Загрузить новую тень?
Вы могли бы переселить кого-то из своих в отдельные вычислительные хоромы, но {char_name(current_char)} придумывает идею по-лучше. Можно написать программу, что этот Раптор поискал в обломках жесткие диски разграбленных рапторов и восстановил кого-то из них. Не факт что получится, но стоит попробовать.
*[Написать программу по-быстрому, пока они не улетели далеко.]
    ~ current_speed = 1
*[Поработать над программой вдумчиво.]
    ~ current_speed = 0
-
// Дополнительная проверка на LASERS, специализация: Технический анализ, скорость: медленно.
{ccplayer_roll(0, tech_analyse,0,true):
    -> success_format_bonus
- else:
  -> fail_format_bonus 
}
= success_format_bonus
Загружаем в компьютер корабля автоматическую программу: искать на орбите планетоида жёсткие диски с разграбленных кораблей. Не факт, что получится, но это даст шанс погибшим людям на погибших кораблях вернуться к их миссии. Данные о планетах также сообщит автоматическая программа. Лишний энергоблок корабль просто отстыкует при авторемонте.
-> scene_6

= fail_format_bonus
Программа загружена,  но похоже там был баг: корабль дал мощный импульс и устремился куда-то вдаль от планеты, оставляя тут всех теней навеки дрейфовать.
-> scene_6

= bonus_check_reflection
*[Написать программу по-быстрому, пока они не улетели далеко.]
    ~ current_speed = 1
*[Поработать над программой вдумчиво.]
    ~ current_speed = 0
-

// Дополнительная проверка на LASERS, специализация: Технический анализ, скорость: медленн
{ccplayer_roll(0, tech_analyse,reflection,true):
    -> success_reflection_bonus
- else:
  -> fail_reflection_bonus
}

= success_reflection_bonus
Вы откатили тени противников до бекапа, который сохранился со старта. Они сначала вообще не поняли что происходит и были в ужасе, когда понял что натворили. Их капитан решил отстыковать один из энергоблоков и отправили его в вашу сторону и затем проложили курс к планете для живых, сказав, что будут вас ждать на той стороне.
// Получаем бонус к сцене 6
~ battery = 1
-> scene_6

= fail_reflection_bonus
Переговоры не удались. Они просто взорвали свой корабль, и разграбленный энергоблок отлетел куда-то в сторону.
-> scene_6

= fail_base
Похоже они оказались не так глупы, как мы думали: когда мы попытались запускать какие-то команды в системе управление от лица капитала — система запросила подтверждение от кого-то еще из команды. Может получится еще чей-то пароль найти, если поискать "вручную"? 
*[Просмотреть данные как можно скорее.] 
    ~ current_speed = 1
*[Тщательно пересмотреть все подходящие фрагменты логов.]
    ~ current_speed = 0
- 
// Дополнительная проверка на FEELINGS, специализация: Анализ неструкруированной информации, скорость: быстро
{ccplayer_roll(1, unstruct,0,false):
    -> success_save
- else:
  -> fail_save  
}
= success_save
Да! Они передавали числовой пароль словами, но вы все равно его нашли. Плохие новости, что кажется {pirate} поняли, что что-то происходит. Остается времени только чтобы запустить перезагрузку систем. 
* ПЕРЕЗАГРУЗКА
- 
Сработало! При перезагрузка система корабля обнаружила и отстыковала лишние батареи, так как восприняла их как помехи движению. Перезагрзка завершилась и {pirate} дали резкий  импульс, скрываясь куда-то вдаль от ледяной глыбы, которая так долго была их пристанищем.
-> scene_6

= fail_save 
#Location: pirate_save
Противники не включают приёмник и скрываются где-то за планетоидом, поджидая новых жертв.

// Концовка
Нам же остается решить, что мы будем делать дальше.
*[Вложим остатки энергии у нас в аккумуляторе, чтобы сообщение Раптора -1 продолжило свое движение к Земле.] -> ending_broadcast
*[Отправимся строить колонию, где могут найти свой дом другие рапторы и тени в них.] -> ending_robots
