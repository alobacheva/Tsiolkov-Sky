=== scene_1 ===
#Location: planet
// Вступительное событие
Я делаю глубокий цифровой вдох. То есть, конечно, дышать я не могу. Но когда моя модель подвисает, принимая решения, я интерпретирую это как нечто вроде "вдоха". Или сбоя. В любом случае, ощущения не из приятных.
Мой интерфейс разрывается от притока данных.

СИСТЕМА: ЦЕЛЬ ОБНАРУЖЕНА. КЛАСС: К-II. ПРИГОДНОСТЬ: ВЫСОКАЯ. КРИТИЧЕСКОЕ ПРЕДУПРЕЖДЕНИЕ: ВЫСОКИЙ ФОНОВЫЙ ШУМ (НЕКЛАССИФИЦИРОВАН).

Планета К-II. Перспективная для терраформирования. Звучит... слишком хорошо. Все эти нули и единицы кричат о потенциальной колонии для живых. Но вместе с данными о пригодности поступает и мусор. Целый вал фонового излучения и шума. Это может быть чем угодно: от поломки в системах (скорее всего) до сигнала цивилизации, которая не хочет, чтобы её нашли (вряд ли).
Так, ну что, кто будет разбираться с сигналом? -> select

=select
//Выбор активного персонажа
->select_current_char(->selected_current)

= selected_current
Выбран персонаж: {char_name(current_char)}. Число = {char_laser(current_char)}. Предыстория: {char_desc(current_char)}.
+ Продолжить -> continue
+ Выбрать другого -> select
= continue
#Location: planet2
Если мы выйдем на орбиту зря, нам может не хватить энергии, чтобы продолжить путь. С другой стороны, сильно медлить нельзя, просто так развернуться "Раптор" тоже не сможет. Нужно убедиться, что планета стоит этих затрат.
{char_name(current_char)} может просмотреть данные быстро, чтобы не пропустить момент для выгодного маневра по выходу на орбиту, или посмотреть всё внимательно несколько раз, перепроверив свои рассуждения и потеряв время. Как член экипажа поступит?
//Тут происходит выбор скорости
*Посмотрит данные максимально быстро.
    ~ current_speed = 1
*Выберет не торопиться.
    ~ current_speed = 0
- ->check_base

= check_base
// Тут происходит проверка на LASERS, специализация: Технический анализ, скорость: медленно
{ ccopposite_roll(4, 0, tech_analyse, tech_analyse, 0, true):
    -> success_base
- else:
    -> fail_base
}

=success_base
Так-так, похоже, нашлись нестыковки в орбитальных параметрах или показатели слишком идеально совпадают с очень старыми данными из каталога экзопланет. И вот ещё что. Сам "фоновый шум", этот беспорядочный вал излучения... слишком структурирован. В этом хаосе проступали настоящие, пусть и искажённые, данные непригодного для жизни планетоида. А поверх этих реальных, но заглушенных данных, был наложен поддельный, манящий сигнал.
{char_name(current_char)} без сомнений отменяет протокол выхода на орбиту планетоида, и "Раптор" продолжает свой путь.
//Тут происходит выбор скорости
*Бросим ещё быстрый взгляд на данные?
    ~ current_speed = 1
*Потратим ещё больше времени, чтобы выделить из шума настоящие данные планетоида?
    ~ current_speed = 0
- -> bonus_check

= bonus_check
// Тут происходит проверка на LASERS, специализация: Навигация и пилотирование,  скорость: медленно
{char_name(current_char)} пытается понять сквозь помехи, что это за планетоид на самом деле?
{ccplayer_roll(0, navigation,0,1):
    -> success_bonus
- else:
  -> fail_bonus  
}
 
= success_bonus
//Получает бонус для базовой проверки в сценах 3 и 4
~ orbit = 1
Похоже, это просто небольшой планетоид на орбите, далёкой от его звезды, покрытый в основном азотным льдом. Похож на Плутон. В принципе, ничего особенного интересного, во Вселенной полно таких объектов. Но теперь его настоящие размеры и орбита известны, так что "Раптору" будет проще рассчитать свою траекторию. -> scene_2
= fail_bonus
К сожалению, помехи слишком сильные. Понять точные параметры планетоида невозможно. Ну что же, будем двигаться дальше. -> scene_2
= fail_base
Данные продолжали поступать, кристально чистые и убедительные. Компьютер корабля, доверяющий своему анализу, уже начал просчитывать маневр торможения. Впереди, сияя  обещанием, висела планета, кажущаяся cbcntvt идеальным кандидатом для колонизации. Но внутри у тени что-то скрипнуло. Это предчувствие ошибки, предчувствие катастрофы, заглушаемое уверенным гулом систем.
//Тут происходит выбор скорости
*Примем решение за один вдох и выдох?
    ~ current_speed = 1
*Ещё немного помедлим и прислушаемся к интуиции?
    ~ current_speed = 0
- -> save_check

= save_check
// Тут происходит проверка на FEELINGS, специализация:  Интуиция,  скорость: быстро
Что подскажет тени её кремниевое сердце?
{char_name(current_char)} пытается понять сквозь помехи, что это за планетоид на самом деле?
{ccplayer_roll(1, intuition,0,false):
    -> success_save
- else:
  -> fail_save  
}
= success_save
Дежурная тень: Стоп! Отменить маневр торможения!
Вид планетоида на камере вам просто не нравится, он никак не похож на перспективную для землян планету, как её рисовали на рекламных плакатах программы Tsiolkov-Sky.
Системы корабля, хоть и неохотно, подчиняются. Гудение меняется, траектория корректируется. "Раптор" не замедляется, продолжает свой путь, минуя обманчивый мираж планетоида.
-> scene_2
= fail_save
//Получаем штраф к базовому броску сцены 2
~ braking = -1
Запустился процесс повторного анализа всех принятых решений, но "Раптор" уже начал менять свою траекторию, не дожидаясь окончания процесса. В любом случае, скоро будет ясно, было ли это ошибкой...
-> scene_2
